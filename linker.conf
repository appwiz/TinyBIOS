# Output file is bios-tmp.elf, that is then changed by objcopy into the
# format we want
#
#
#STARTUP(reset.o)
ENTRY(reset)

# Code section definitions, see memory map for reference with different
# regions and stuff
#
# 00000000 - 000003FF -- IVT
# 00000400 - 000004FF -- BDA
# 00000500 - 00007BFF -- FREE
# 00007C00 - 00007DFF -- Boot sector
# 00007E00 - 0007FFFF -- FREE
# 00080000 - 0009FFFF -- EBDA
# 000A0000 ->         -- MMIO
#
MEMORY {
    mem_ram (rw)       : org = 0, len = 0x9FFFF
    mem_rom_low (rx)    : org = 0xE0000, len = 0x0FFFF
    mem_rom_high (rx)   : org = 0xF0000, len = 0x10000
}

SECTIONS {
    . = 0x700;
    .stack : {
        PROVIDE (__stack = .);
    } > mem_ram
    .bss : { 
        *(.bss)
    } > mem_ram

    .text 0x1000 : AT (0xe0000) {
        *(.data)
        *(.interp)
        *(.dynsym)
        *(.gnu.hash)
        *(.rodata)
        *(.plt)
        *(.dynstr)
        *(.rodata*)
        *(.dyn*)
        *(.rel*)
        *(.text)
    } 

    . = 0xF0000;
    .rom_text 0xF0000 : AT (0xF0000) {
        *(.rom_text)
        . = 0x0FFF0;
        *(.reset)
        . = ALIGN(16);
    } > mem_rom_high =0xFF
}

